!-----------------------------------------------------------------------------------------------------------------------------------
program nc_print ! @(#) print an ncurses(3c) window dump as HTML
   use ncurses                                  
   use M_kracken
   implicit none
   integer :: ierr
   logical :: paws=.false.
   character(255) filename_in
   character(255) filename_out
   type(C_PTR) :: win
!-----------------------------------------------------------------------------------------------------------------------------------
!  define command arguments, default values and crack command line
   call kracken('cmd','-i  -o paper.html -pg .false. -help .false. -version .false.')
   if(lget('cmd_help'))then
      write(*,*)'--------------------------------------------------------------------------------'
      write(*,*)'Given a file generated with the ncurses(3c) putwin(3c) procedure'
      write(*,*)'read the file and write it out an an HTML file for printing or '
      write(*,*)'for inclusion much like a IMG file into an HTML document.'
      write(*,*)''
      write(*,*)'    ncp -i INPUT_FILE -o OUTPUT_FILE [-pg]'
      write(*,*)'   -i INPUT_FILE'
      write(*,*)'      Name of ncurses(3c) window dump file generated by putwin(3c).'
      write(*,*)'   -o OUTPUT)FILE'
      write(*,*)'      Name of HTML file to generate.'
      write(*,*)'   -pg'
      write(*,*)'       Display the ncurses(3c) window dump file and pause for user'
      write(*,*)''
      write(*,*)'--------------------------------------------------------------------------------'
      stop
   endif
   filename_in=sget('cmd_i')
   if(filename_in.eq.'')then
      filename_in=sget('cmd_oo')
   endif
   filename_out=sget('cmd_o')
   paws=lget('cmd_pg')
   write(6,*)'INPUT:    ',trim(filename_in)
   write(6,*)'OUTPUT:   ',trim(filename_out)
   flush(unit=6)
!-----------------------------------------------------------------------------------------------------------------------------------
   stdscr=initscr()                             
   ierr=start_color()
   ! unfortunately, a putwin(3c) file does not appear to contain the color definitions from the originating program?
   ! define common default color pairs
   ierr=init_pair(1_C_SHORT, COLOR_RED,     COLOR_BLACK)
   ierr=init_pair(2_C_SHORT, COLOR_GREEN,   COLOR_BLACK)
   ierr=init_pair(3_C_SHORT, COLOR_YELLOW,  COLOR_BLACK)
   ierr=init_pair(4_C_SHORT, COLOR_BLUE,    COLOR_BLACK)
   ierr=init_pair(5_C_SHORT, COLOR_CYAN,    COLOR_BLACK)
   ierr=init_pair(6_C_SHORT, COLOR_MAGENTA, COLOR_BLACK)
   ierr=init_pair(7_C_SHORT, COLOR_WHITE,   COLOR_BLACK)
   !! seems to be OK with reading windows larger than current display. Not clear to me that would be the case. Maybe use pads.
   win = getwin(trim(filename_in)//C_NULL_CHAR) ! read the window data
   if (.not.c_associated(win)) then
      ierr=endwin()
      write(*,*)"Unable to read/create window"
      stop
   endif
   if(paws)then
      ierr=refresh()
      ierr=wrefresh(win)                               
      ierr=getch()
   endif
   call nc_printhtml(win,filename_out)
   ierr=delwin(win)
   ierr=endwin()                                
end program nc_print
!-----------------------------------------------------------------------------------------------------------------------------------
